# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

from math.math import _Expable, exp
from random import randn_float64, seed
from sys import CompilationTarget

from test_utils import libm_call
from testing import assert_almost_equal, assert_equal, TestSuite


def test_exp_bfloat16():
    assert_equal(exp(BFloat16(2.0)), 7.375)
    assert_equal(exp(5.0) * exp(6.0), exp(5.0 + 6.0))
    assert_equal(exp(5.0) / exp(2.0), exp(5.0 - 2.0))


def test_exp_float16():
    assert_almost_equal(exp(Float16(-0.1)), 0.9047)
    assert_almost_equal(exp(Float16(0.1)), 1.105)
    assert_almost_equal(exp(Float16(2)), 7.389)
    assert_equal(String(exp(Float16(89))), "inf")
    assert_equal(String(exp(Float16(108.5230))), "inf")


def test_exp_float32():
    assert_almost_equal(exp(Float32(-0.1)), 0.90483)
    assert_almost_equal(exp(Float32(0.1)), 1.10517)
    assert_almost_equal(exp(Float32(2)), 7.38905)
    assert_equal(String(exp(Float32(89))), "inf")
    assert_equal(String(exp(Float32(108.5230))), "inf")


def test_exp_float64():
    assert_almost_equal(exp(Float64(-0.1)), 0.90483)
    assert_almost_equal(exp(Float64(0.1)), 1.10517)
    assert_almost_equal(exp(Float64(2)), 7.38905)
    # FIXME (40568) should remove str
    assert_equal(String(exp(Float64(89))), String(4.4896128193366053e38))
    assert_equal(String(exp(Float64(108.5230))), String(1.3518859659123633e47))


@always_inline
def exp_libm[
    dtype: DType, simd_width: Int
](arg: SIMD[dtype, simd_width]) -> SIMD[dtype, simd_width]:
    return libm_call["expf", "exp"](arg)


def _test_exp_libm[dtype: DType]() where dtype.is_floating_point():
    seed(0)
    comptime N = 8192
    for _i in range(N):
        var x = randn_float64(0, 9.0).cast[dtype]()
        assert_almost_equal(
            exp(x), exp_libm(x), msg=String("for the input ", x)
        )


def test_exp_libm():
    _test_exp_libm[DType.float32]()
    _test_exp_libm[DType.float64]()


@fieldwise_init
struct Float32Expable(Equatable, Stringable, _Expable):
    """This is a test struct that implements the Expable trait for Float32."""

    var x: Float32

    fn __exp__(self) -> Self:
        return Self(exp(self.x))

    fn __eq__(self, other: Self) -> Bool:
        return self.x == other.x

    fn __ne__(self, other: Self) -> Bool:
        return self.x != other.x

    fn __str__(self) -> String:
        return String("Float32Expable(", self.x, ")")


@fieldwise_init
struct FakeExpable(Equatable, Stringable, _Expable):
    """Test struct using default reflection-based __eq__."""

    var x: Int

    fn __exp__(self) -> Self:
        return Self(99)

    # Uses default reflection-based __eq__ from Equatable trait

    fn __str__(self) -> String:
        return String("FakeExpable(", self.x, ")")


def test_exapble_trait():
    assert_equal(exp(Float32Expable(1.0)), Float32Expable(exp(Float32(1.0))))
    assert_equal(exp(FakeExpable(1)), FakeExpable(99))


def main():
    TestSuite.discover_tests[__functions_in_module()]().run()

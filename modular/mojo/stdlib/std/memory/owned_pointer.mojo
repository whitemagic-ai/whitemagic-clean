# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
"""Implements `OwnedPointer`, a safe, single-ownership smart pointer.

You can import these APIs from the `memory` package. For example:

```mojo
from memory import OwnedPointer
```
"""

from builtin.constrained import _constrained_conforms_to
from builtin.rebind import downcast, trait_downcast
from format._utils import (
    Repr,
    FormatStruct,
    TypeNames,
    constrained_conforms_to_writable,
)


struct OwnedPointer[T: AnyType](RegisterPassable, Writable):
    """A safe, owning, smart pointer.

    This smart pointer is designed for cases where there is clear ownership
    of the underlying data, and restricts access to it through the origin
    system such that no more than one mutable alias for the underlying data
    may exist.

    For a comparison with other pointer types, see [Intro to
    pointers](/mojo/manual/pointers/) in the Mojo Manual.

    Parameters:
        T: The type to be stored in the `OwnedPointer`.
    """

    var _inner: UnsafePointer[Self.T, MutExternalOrigin]

    # ===-------------------------------------------------------------------===#
    # Life cycle methods
    # ===-------------------------------------------------------------------===#

    fn __init__[_T: Movable](out self: OwnedPointer[_T], var value: _T):
        """Construct a new `OwnedPointer` by moving the passed value into a new backing allocation.

        Parameters:
            _T: The type of the data to store. It is restricted to `Movable` here to allow efficient move construction.

        Args:
            value: The value to move into the `OwnedPointer`.
        """
        self._inner = alloc[_T](1)
        self._inner.init_pointee_move(value^)

    fn __init__[_T: Copyable](out self: OwnedPointer[_T], *, copy_value: _T):
        """Construct a new `OwnedPointer` by explicitly copying the passed value into a new backing allocation.

        Parameters:
            _T: The type of the data to store, which must be
               `Copyable`.

        Args:
            copy_value: The value to explicitly copy into the `OwnedPointer`.
        """
        self._inner = alloc[_T](1)
        self._inner.init_pointee_copy(copy_value)

    fn __init__[
        _T: Copyable, U: NoneType = None
    ](out self: OwnedPointer[_T], value: _T):
        """Construct a new `OwnedPointer` by copying the passed value into a new backing allocation.

        Parameters:
            _T: The type of the data to store.
            U: A dummy type parameter, to lower the selection priority of this ctor.

        Args:
            value: The value to copy into the `OwnedPointer`.
        """
        self._inner = alloc[_T](1)
        self._inner.init_pointee_copy(value)

    fn __init__[
        _T: Copyable
    ](out self: OwnedPointer[_T], *, other: OwnedPointer[_T]):
        """Construct a new `OwnedPointer` by explicitly copying the value from another `OwnedPointer`.

        Parameters:
            _T: The type of the data to store.

        Args:
            other: The `OwnedPointer` to copy.
        """
        self = OwnedPointer[_T](copy_value=other[])

    fn __init__(
        out self,
        *,
        unsafe_from_raw_pointer: UnsafePointer[Self.T, MutExternalOrigin],
    ):
        """Construct a new `OwnedPointer` by taking ownership of the provided `UnsafePointer`.

        Args:
            unsafe_from_raw_pointer: The `UnsafePointer` to take ownership of.

        Safety:

        This function is unsafe as the provided `UnsafePointer` must be initialize with a single valid `T`
        initially allocated with this `OwnedPointer`'s backing allocator.
        This function is unsafe as other memory problems can arise such as a double-free if this function
        is called twice with the same pointer or a user manually deallocates the same data.

        After using this constructor, the `UnsafePointer` is assumed to be owned by this `OwnedPointer`.
        In particular, the destructor method will call `T.__del__` and `UnsafePointer.free`.
        """
        self._inner = unsafe_from_raw_pointer

    fn __init__(out self, *, unsafe_from_opaque_pointer: MutOpaquePointer[_]):
        """Construct a new `OwnedPointer` by taking ownership of the provided `UnsafePointer`.

        Args:
            unsafe_from_opaque_pointer: The `OpaquePointer` to take ownership of.

        Safety:

        This function is unsafe as the provided `OpaquePointer` must be initialize with a single valid `T`
        initially allocated with this `OwnedPointer`'s backing allocator.
        This function is unsafe as other memory problems can arise such as a double-free if this function
        is called twice with the same pointer or a user manually deallocates the same data.

        After using this constructor, the `UnsafePointer` is assumed to be owned by this `OwnedPointer`.
        In particular, the destructor method will call `T.__del__` and `UnsafePointer.free`.
        """
        var ptr = unsafe_from_opaque_pointer.bitcast[Self.T]()
        self = Self(
            unsafe_from_raw_pointer=ptr.unsafe_origin_cast[MutExternalOrigin]()
        )

    fn __del__(deinit self):
        """Destroy the OwnedPointer[]."""
        _constrained_conforms_to[
            conforms_to(Self.T, ImplicitlyDestructible),
            Parent=Self,
            Element = Self.T,
            ParentConformsTo="ImplicitlyDestructible",
        ]()
        comptime TDestructible = downcast[Self.T, ImplicitlyDestructible]

        self._inner.bitcast[TDestructible]().destroy_pointee()
        self._inner.free()

    # ===-------------------------------------------------------------------===#
    # Operator dunders
    # ===-------------------------------------------------------------------===#

    fn __getitem__(
        ref[AddressSpace.GENERIC] self,
    ) -> ref[self, AddressSpace.GENERIC] Self.T:
        """Returns a reference to the pointers's underlying data with parametric mutability.

        Returns:
            A reference to the data underlying the `OwnedPointer`.
        """
        # This should have a widening conversion here that allows
        # the mutable ref that is always (potentially unsafely)
        # returned from UnsafePointer to be guarded behind the
        # aliasing guarantees of the origin system here.
        # All of the magic happens above in the function signature
        return self._inner[]

    # ===-------------------------------------------------------------------===#
    # Methods
    # ===-------------------------------------------------------------------===#

    fn unsafe_ptr[
        mut: Bool,
        origin: Origin[mut=mut],
        //,
    ](ref[origin] self) -> UnsafePointer[Self.T, origin]:
        """Returns the backing pointer for this `OwnedPointer`.

        Parameters:
            mut: Whether the pointer is mutable.
            origin: The origin of the pointer.

        Returns:
            An UnsafePointer to the backing allocation for this `OwnedPointer`.
        """
        return self._inner.mut_cast[mut]().unsafe_origin_cast[origin]()

    fn take[_T: Movable](deinit self: OwnedPointer[_T]) -> _T:
        """Move the value within the `OwnedPointer` out of it, consuming the
        `OwnedPointer` in the process.

        Parameters:
            _T: The type of the data backing this `OwnedPointer`. `take()` only exists for `T: Movable`
                since this consuming operation only makes sense for types that you want to avoid copying.
                For types that are `ImplicitlyCopyable` or `Copyable` you can copy them through
                `__getitem__` as in `var v = some_ptr_var[]`.

        Returns:
            The data that is (was) backing the `OwnedPointer`.
        """
        var r = self._inner.take_pointee()
        self._inner.free()
        return r^

    fn steal_data(deinit self) -> UnsafePointer[Self.T, MutExternalOrigin]:
        """Take ownership over the heap allocated pointer backing this
        `OwnedPointer`.

        Safety:

        This function is not unsafe to call, as a memory leak is not
        considered unsafe.

        However, to avoid a memory leak, callers should ensure that the
        returned pointer is eventually deinitialized and deallocated.
        Failure to do so will leak memory.

        Returns:
            The pointer owned by this instance.
        """
        return self._inner

    fn write_to(self, mut writer: Some[Writer]):
        """Formats this pointer's value to the provided Writer.

        Args:
            writer: The object to write to.

        Constraints:
            `T` must conform to Writable.
        """
        _constrained_conforms_to[
            conforms_to(Self.T, Writable),
            Parent=Self,
            Element = Self.T,
            ParentConformsTo="Writable",
        ]()
        trait_downcast[Writable](self[]).write_to(writer)

    fn write_repr_to(self, mut writer: Some[Writer]):
        """Write the string representation of the `OwnedPointer`.

        Args:
            writer: The object to write to.

        Constraints:
            `T` must conform to Writable.
        """
        constrained_conforms_to_writable[Self.T, Parent=Self]()
        FormatStruct(writer, "OwnedPointer").params(
            TypeNames[Self.T](),
        ).fields(Repr(trait_downcast[Writable](self[])))

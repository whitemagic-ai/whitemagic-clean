# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

# Use `kgen --emit=asm %s -o %t.asm` to exam the assembly code.

from math import ceildiv
from sys.info import simd_width_of

from layout import IntTuple, LayoutTensor, Layout, RuntimeLayout
from nn.conv import ConvDirectNHWC, ConvInfoStatic
from nn.conv_utils import (
    ConvShape,
    get_direct_conv_micro_kernel_width,
    get_micro_kernel_shape,
)

from utils.index import Index

comptime N = 1
comptime H = 14
comptime W = 14
comptime C = 8
comptime R = 3
comptime S = 3
comptime F = 8
comptime stride_h = 1
comptime stride_w = 1
comptime pad_left = 1
comptime pad_right = 1
comptime pad_top = 1
comptime pad_bottom = 1
comptime dilation_h = 1
comptime dilation_w = 1
comptime HO = (
    H + pad_left + pad_right - dilation_h * (R - 1) - 1
) // stride_h + 1
comptime WO = (
    W + pad_top + pad_bottom - dilation_w * (S - 1) - 1
) // stride_w + 1
comptime num_groups = 1

comptime conv_attr = ConvInfoStatic[2](
    IntTuple(pad_bottom, pad_left, pad_top, pad_right),
    IntTuple(stride_h, stride_w),
    IntTuple(dilation_h, dilation_w),
    num_groups,
)

comptime value_type = DType.float32
comptime simd_size = simd_width_of[value_type]()
comptime micro_kernel_shape = get_micro_kernel_shape[
    2, WO, F, conv_attr, simd_size
]()
# alias micro_kernel_width = get_direct_conv_micro_kernel_width()
comptime micro_kernel_f_size = micro_kernel_shape[1] * simd_size
comptime num_micro_tile = ceildiv(F, micro_kernel_f_size)


fn static_conv(
    output: LayoutTensor[mut=True, value_type, Layout.row_major(N, HO, WO, F)],
    input: LayoutTensor[value_type, Layout.row_major(N, H, W, C)],
    filter: LayoutTensor[
        value_type,
        Layout.row_major(num_micro_tile, R, S, C, micro_kernel_f_size),
    ],
):
    var conv_shape = ConvShape[2](
        n=N,
        input_dims=Index(H, W),
        output_dims=Index(HO, WO),
        filter_dims=Index(R, S),
        c=C,
        f=F,
        stride=Index(stride_h, stride_w),
        dilation=Index(dilation_h, dilation_w),
        pad_d=Index(0, 0),
        pad_h=Index(pad_bottom, pad_top),
        pad_w=Index(pad_left, pad_right),
        num_groups=num_groups,
    )

    fn direct_null_elementwise_epilogue(
        n: Int, ho: Int, wo: Int, f_offset: Int, f_size: Int
    ):
        pass

    try:
        ConvDirectNHWC[
            Layout.row_major(N, H, W, C),
            Layout.row_major(num_micro_tile, R, S, C, micro_kernel_f_size),
            Layout.row_major(N, HO, WO, F),
            _,
            _,
            _,
            value_type,
            value_type,
            value_type,
            True,
            conv_attr,
        ].run(output, input, filter, conv_shape)
    except e:
        print(e)


# CHECK-LABEL: test_static_conv
def test_static_conv():
    print("== test_static_conv")

    var output_stack = InlineArray[Scalar[value_type], N * HO * WO * F](
        uninitialized=True
    )
    var output = LayoutTensor[value_type, Layout.row_major(N, HO, WO, F)](
        output_stack
    ).fill(0.0)
    var input_stack = InlineArray[Scalar[value_type], N * H * W * C](
        uninitialized=True
    )
    var input = LayoutTensor[value_type, Layout.row_major(N, H, W, C)](
        input_stack
    ).fill(1.0)
    var filter_stack = InlineArray[
        Scalar[value_type], num_micro_tile * R * S * C * micro_kernel_f_size
    ](uninitialized=True)
    var filter = LayoutTensor[
        value_type,
        Layout.row_major(num_micro_tile, R, S, C, micro_kernel_f_size),
    ](filter_stack).fill(1.0)

    static_conv(output, input, filter)

    # CHECK: 32.0
    print(output[0, 0, 0, 0])


def main():
    test_static_conv()

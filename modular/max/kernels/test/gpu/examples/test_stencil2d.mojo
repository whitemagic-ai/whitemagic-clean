# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

from math import ceildiv

from buffer import NDBuffer
from buffer.dimlist import DimList
from gpu import barrier, block_dim, global_idx, thread_idx
from gpu.host import DeviceContext

from utils.index import Index

comptime BLOCK_DIM = 4


fn stencil2d(
    a_ptr: UnsafePointer[Float32, MutAnyOrigin],
    b_ptr: UnsafePointer[Float32, MutAnyOrigin],
    arr_size: Int,
    num_rows: Int,
    num_cols: Int,
    coeff0: Int,
    coeff1: Int,
    coeff2: Int,
    coeff3: Int,
    coeff4: Int,
):
    var tidx = global_idx.x
    var tidy = global_idx.y

    var a = NDBuffer[DType.float32, 1](a_ptr, Index(arr_size))
    var b = NDBuffer[DType.float32, 1](b_ptr, Index(arr_size))

    if (
        tidy > 0
        and tidx > 0
        and tidy < UInt(num_rows - 1)
        and tidx < UInt(num_cols - 1)
    ):
        var idx = Int(tidy * UInt(num_cols) + tidx)
        b[idx] = (
            Float32(coeff0) * a[idx - 1]
            + Float32(coeff1) * a[idx]
            + Float32(coeff2) * a[idx + 1]
            + Float32(coeff3) * a[Int((tidy - 1) * UInt(num_cols) + tidx)]
            + Float32(coeff4) * a[Int((tidy + 1) * UInt(num_cols) + tidx)]
        )


fn stencil2d_smem(
    a_ptr: UnsafePointer[Float32, MutAnyOrigin],
    b_ptr: UnsafePointer[Float32, MutAnyOrigin],
    arr_size: Int,
    num_rows: Int,
    num_cols: Int,
    coeff0: Int,
    coeff1: Int,
    coeff2: Int,
    coeff3: Int,
    coeff4: Int,
):
    var tidx = global_idx.x
    var tidy = global_idx.y
    var lindex_x = thread_idx.x + 1
    var lindex_y = thread_idx.y + 1

    var a = NDBuffer[DType.float32, 1](a_ptr, Index(arr_size))
    var b = NDBuffer[DType.float32, 1](b_ptr, Index(arr_size))

    var a_shared = NDBuffer[
        DType.float32,
        2,
        MutAnyOrigin,
        DimList(BLOCK_DIM + 2, BLOCK_DIM + 2),
        address_space = AddressSpace.SHARED,
    ].stack_allocation()

    # Each element is loaded in shared memory.
    a_shared[Index(lindex_y, lindex_x)] = a[Int(tidy * UInt(num_cols) + tidx)]

    # First column also loads elements left and right to the block.
    if thread_idx.x == 0:
        var idx = Int(tidy * UInt(num_cols) + (tidx - 1))
        a_shared[Index(lindex_y, 0)] = a[idx] if 0 <= idx < arr_size else 0

        idx = Int(tidy * UInt(num_cols) + tidx + BLOCK_DIM)
        a_shared[Index(Int(lindex_y), BLOCK_DIM + 1)] = (
            a[idx] if 0 <= idx < arr_size else 0
        )

    # First row also loads elements above and below the block.
    if thread_idx.y == 0:
        var idx = Int((tidy - 1) * UInt(num_cols) + tidx)
        a_shared[Index(0, lindex_x)] = a[idx] if 0 < idx < arr_size else 0

        idx = Int((tidy + BLOCK_DIM) * UInt(num_cols) + tidx)
        a_shared[Index(BLOCK_DIM + 1, lindex_x)] = (
            a[idx] if 0 <= idx < arr_size else 0
        )

    barrier()

    if (
        tidy > 0
        and tidx > 0
        and tidy < UInt(num_rows - 1)
        and tidx < UInt(num_cols - 1)
    ):
        b[Int(tidy * UInt(num_cols) + tidx)] = (
            Float32(coeff0) * a_shared[Index(lindex_y, lindex_x - 1)]
            + Float32(coeff1) * a_shared[Index(lindex_y, lindex_x)]
            + Float32(coeff2) * a_shared[Index(lindex_y, lindex_x + 1)]
            + Float32(coeff3) * a_shared[Index(lindex_y - 1, lindex_x)]
            + Float32(coeff4) * a_shared[Index(lindex_y + 1, lindex_x)]
        )


# CHECK-LABEL: run_stencil2d
fn run_stencil2d[smem: Bool](ctx: DeviceContext) raises:
    print("== run_stencil2d")

    comptime m = 64
    comptime coeff0 = 3
    comptime coeff1 = 2
    comptime coeff2 = 4
    comptime coeff3 = 1
    comptime coeff4 = 5
    comptime iterations = 4

    comptime num_rows = 8
    comptime num_cols = 8

    var a_host = alloc[Float32](m)
    var b_host = alloc[Float32](m)

    for i in range(m):
        a_host[i] = Float32(i)
        b_host[i] = 0

    var a_device = ctx.enqueue_create_buffer[DType.float32](m)
    var b_device = ctx.enqueue_create_buffer[DType.float32](m)

    ctx.enqueue_copy(a_device, a_host)
    ctx.enqueue_copy(b_device, b_host)

    comptime func_select = stencil2d_smem if smem == True else stencil2d

    for _ in range(iterations):
        ctx.enqueue_function_experimental[func_select](
            a_device,
            b_device,
            m,
            num_rows,
            num_cols,
            coeff0,
            coeff1,
            coeff2,
            coeff3,
            coeff4,
            grid_dim=(
                ceildiv(num_rows, BLOCK_DIM),
                ceildiv(num_cols, BLOCK_DIM),
            ),
            block_dim=(BLOCK_DIM, BLOCK_DIM),
        )

        var tmp_ptr = b_device
        b_device = a_device
        a_device = tmp_ptr

    ctx.enqueue_copy(b_host, b_device)
    ctx.synchronize()

    # CHECK: 37729.0 ,52628.0 ,57021.0 ,60037.0 ,58925.0 ,39597.0 ,
    # CHECK: 57888.0 ,80505.0 ,86322.0 ,89682.0 ,86994.0 ,57818.0 ,
    # CHECK: 76680.0 ,106488.0 ,113400.0 ,116775.0 ,112182.0 ,73933.0 ,
    # CHECK: 95424.0 ,132408.0 ,140400.0 ,143775.0 ,137262.0 ,89925.0 ,
    # CHECK: 91968.0 ,135753.0 ,144450.0 ,147450.0 ,138642.0 ,81842.0 ,
    # CHECK: 50277.0 ,73628.0 ,81985.0 ,83565.0 ,71417.0 ,43229.0 ,
    for i in range(1, num_rows - 1):
        for j in range(1, num_cols - 1):
            print(b_host[i * num_cols + j], ",", end="")
        print()

    _ = a_device
    _ = b_device

    _ = a_host
    _ = b_host


def main():
    with DeviceContext() as ctx:
        run_stencil2d[False](ctx)
        run_stencil2d[True](ctx)
